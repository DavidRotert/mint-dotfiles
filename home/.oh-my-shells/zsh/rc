# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

load_zsh_plugin() {
    plugin_name="$1"
    possible_plugin_file_locations=(
        "/usr/share/zsh/plugins/$plugin_name.plugin.zsh"
        "/usr/share/zsh/plugins/$plugin_name.zsh"
        "/usr/share/zsh/plugins/$plugin_name/$plugin_name.plugin.zsh"
        "/usr/share/zsh/plugins/$plugin_name/$plugin_name.zsh"
        "/usr/share/$plugin_name/$plugin_name.plugin.zsh"
        "/usr/share/$plugin_name/$plugin_name.zsh"
    )

    for plugin_file in $possible_plugin_file_locations
    do
        if [ -f "$plugin_file" ]
        then        
            source "$plugin_file"
            return 0
        fi
    done
    echo "ZSH plugin '$plugin_name' could not be loaded" > /dev/stderr
}

load_zsh_plugin zsh-autosuggestions
load_zsh_plugin zsh-syntax-highlighting

# ZSH built in autocomplete
autoload -Uz compinit
compinit

zstyle ':completion:*' menu select

# Autocorrection
## Fix autocorrection
alias cp='nocorrect cp'
alias ebuild='nocorrect ebuild'
alias gist='nocorrect gist'
alias heroku='nocorrect heroku'
alias hpodder='nocorrect hpodder'
alias man='nocorrect man'
alias mkdir='nocorrect mkdir'
alias mv='nocorrect mv'
alias mysql='nocorrect mysql'
alias sudo='nocorrect sudo'

setopt correct_all

# History configuration
export HISTFILE="$HOME/.zsh_history"
export HISTSIZE=1000
export SAVEHIST=2000

## History command configuration
setopt hist_expire_dups_first # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt hist_ignore_dups # ignore duplicated commands history list
setopt hist_ignore_space # ignore commands that start with space
setopt hist_verify # show command with history expansion to user before running it
setopt share_history # share command history data

clear-history() {
    echo "" > $HISTFILE
    history -p
}

# Directory config
## Changing/making/removing directory
setopt auto_pushd
setopt pushd_ignore_dups
setopt pushdminus

# Mode
bindkey -e

## Keybindings
bindkey "^[[H" beginning-of-line # home key
bindkey "^[[F" end-of-line # end key
bindkey "^[[1;5C" forward-word
bindkey "^[[1;5D" backward-word

## Edit line with ctrl-l
autoload edit-command-line
zle -N edit-command-line
bindkey "^l" edit-command-line
bindkey "^e" edit-command-line

PS1="%K{green}%F{black}%n@%m %k%f%K{blue}%F{black} %~ %k%f%B%K{white}%b%F{black}$%b%k%f "

if [ -f ~/.zsh_prompt_custom ]
then
    . ~/.zsh_prompt_custom
else
    source /usr/local/share/zsh/themes/powerlevel10k/powerlevel10k.zsh-theme
    # To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
    [[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
fi
