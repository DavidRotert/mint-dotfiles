local __prompt_theme="oh-my-posh"

if [ "$__prompt_theme" = "p10k" ]
then
    # Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
    # Initialization code that may require console input (password prompts, [y/n]
    # confirmations, etc.) must go above this block; everything else may go below.
    if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]
    then
        source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
    fi
fi

source_if_exists() {
    [ -f "$1" ] && source "$1"
}

load_zsh_plugin() {
    local __plugin_name="$1"
    local __possible_plugin_file_locations=(
        "/usr/local/share/zsh/plugins/$__plugin_name.plugin.zsh"
        "/usr/local/share/zsh/plugins/$__plugin_name.zsh"
        "/usr/local/share/zsh/plugins/$__plugin_name/$__plugin_name.plugin.zsh"
        "/usr/local/share/zsh/plugins/$__plugin_name/$__plugin_name.zsh"
        "/usr/local/share/$__plugin_name/$__plugin_name.plugin.zsh"
        "/usr/local/share/$__plugin_name/$__plugin_name.zsh"

        "/usr/share/zsh/plugins/$__plugin_name.plugin.zsh"
        "/usr/share/zsh/plugins/$__plugin_name.zsh"
        "/usr/share/zsh/plugins/$__plugin_name/$__plugin_name.plugin.zsh"
        "/usr/share/zsh/plugins/$__plugin_name/$__plugin_name.zsh"
        "/usr/share/$__plugin_name/$__plugin_name.plugin.zsh"
        "/usr/share/$__plugin_name/$__plugin_name.zsh"
    )

    for __plugin_file in $__possible_plugin_file_locations
    do
        if [ -f "$__plugin_file" ]
        then        
            source "$__plugin_file"
            return 0
        fi
    done
    echo "ZSH plugin '$__plugin_name' could not be loaded" > /dev/stderr
}

local __plugins=(zsh-autosuggestions zsh-syntax-highlighting)

for __plugin in $__plugins
do
    load_zsh_plugin "$__plugin"
done

# ZSH built in autocomplete
autoload -Uz compinit
compinit

zstyle ':completion:*' menu select

# Autocorrection
## Fix autocorrection
alias cp='nocorrect cp'
alias ebuild='nocorrect ebuild'
alias gist='nocorrect gist'
alias heroku='nocorrect heroku'
alias hpodder='nocorrect hpodder'
alias man='nocorrect man'
alias mkdir='nocorrect mkdir'
alias mv='nocorrect mv'
alias mysql='nocorrect mysql'
alias sudo='nocorrect sudo'

setopt correct_all

# History configuration
HISTFILE="$HOME/.zsh_history"
HISTSIZE=1000
SAVEHIST=2000

## History command configuration
setopt hist_expire_dups_first # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt hist_ignore_dups # ignore duplicated commands history list
setopt hist_save_no_dups
#setopt hist_ignore_space # ignore commands that start with space
setopt hist_verify # show command with history expansion to user before running it
setopt share_history # share command history data
#setopt inc_append_history

clear-history() {
    echo "" > $HISTFILE
    history -p
}

# Directory config
## Changing/making/removing directory
setopt auto_pushd
setopt pushd_ignore_dups
setopt pushdminus

# Mode
bindkey -e

## Keybindings
bindkey "^[[H" beginning-of-line # home key
bindkey "^[[F" end-of-line # end key
bindkey "^[[1;5C" forward-word
bindkey "^[[1;5D" backward-word

## Edit line with ctrl-l
autoload edit-command-line
zle -N edit-command-line
bindkey "^e" edit-command-line

source_if_exists /usr/share/doc/fzf/examples/key-bindings.zsh

#PS1="%K{green}%F{black}%n@%m %k%f%K{blue}%F{black} %~ %k%f%B%K{white}%b%F{black}$%b%k%f "

if [ "$__prompt_theme" = "p10k" ] && [ -f "/usr/local/share/zsh/themes/powerlevel10k/powerlevel10k.zsh-theme" ]
then
    source /usr/local/share/zsh/themes/powerlevel10k/powerlevel10k.zsh-theme
    # To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
    source_if_exists ~/.p10k.zsh
elif [ "$__prompt_theme" = "oh-my-posh" ] && command -v "oh-my-posh" &>/dev/null
then
    eval "$(oh-my-posh init zsh --config ~/.config/oh-my-posh.yml)"
fi

source_if_exists "$HOME/.zshrc_custom"

source_if_exists "$HOME/.aliases"
source_if_exists "$HOME/.aliases_custom"
